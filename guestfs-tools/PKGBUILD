# Maintainer: Daniel Pereira <daniel@garajau.com.br>
# Copied from: https://gitlab.com/Toolybird/pkgbuilds/-/blob/main/guestfs-tools/PKGBUILD

pkgname=guestfs-tools
pkgver=1.46.1
pkgrel=1
pkgdesc="Tools for accessing and modifying guest disk images"
arch=(x86_64)
url="http://libguestfs.org/"
license=(GPL2 LGPL2.1)
depends=(
  jansson
  libguestfs libguestfs.so
  libvirt libvirt.so
  libxcrypt libcrypt.so
  libxml2 libxml2.so
  ncurses libncursesw.so
  pcre2
  perl-libintl-perl
  xz
)
makedepends=(bash-completion libisoburn ocaml ocaml-findlib perl-module-build)
backup=(
  etc/virt-builder/repos.d/libguestfs.conf
  etc/virt-builder/repos.d/libguestfs.gpg
  etc/virt-builder/repos.d/opensuse.conf
  etc/virt-builder/repos.d/opensuse.gpg
)
source=("http://download.libguestfs.org/$pkgname/${pkgver%.*}-stable/$pkgname-$pkgver.tar.gz"{,.sig})
validpgpkeys=(F7774FB1AD074A7E8C8767EA91738F73E1B768A0) # Richard W.M. Jones <rjones@redhat.com>
sha256sums=('93acc3aeaeda102c7134b40eb3b59bf3ef7d06f5cd44245ec1401bec77ba36a8'
            'SKIP')

prepare() {
  cd $pkgname-$pkgver
  # quick hack to fix build
  sed -i 's|CDEFLMPSUVYZX+52-3|+C+D+E+F+L+M+P+S+U+V+Y+Z+X+52-3-6 -w -6|' configure
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
#
# the test suite is slow AF unless /dev/kvm is available inside the nspawn
# container e.g. pass "-D /dev/kvm" to makechrootpkg
#
# 1 test will be skipped if unable to access /dev/fuse
#
# 2 tests fail that want librpm, skip both
#
  cd $pkgname-$pkgver
  SKIP_TEST_VIRT_INSPECTOR_SH=1 \
  SKIP_TEST_VIRT_INSPECTOR_LUKS_SH=1 \
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
