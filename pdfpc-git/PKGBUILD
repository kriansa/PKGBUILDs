# Maintainer: Daniel Pereira <daniel@garajau.com.br>
# Contributor: Jiachen Yang <farseerfc@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Andreas Bilke <andreas@bilke>

pkgname=pdfpc-git
pkgver=4.5.0.r136.g135282d
pkgrel=1
pkgdesc="Presenter console with multi-monitor support for PDF files"
arch=(x86_64)
url="https://pdfpc.github.io/"
license=(GPL)
depends=(gtk3 poppler-glib libgee gstreamer gst-plugins-base webkit2gtk discount)
optdepends=('gst-plugin-gtk: for video playback'
            'gst-plugins-good: more codecs for video playback support'
            'gst-libav: more codecs for video playback support')
makedepends=(cmake vala)
source=("$pkgname::git+https://github.com/pdfpc/pdfpc")
sha256sums=(SKIP)

pkgver() {
  cd $pkgname || exit 1
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  make -C build
}

package() {
  make -C build DESTDIR="$pkgdir" install
}
